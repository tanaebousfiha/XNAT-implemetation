import org.gradle.internal.jvm.Jvm

buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
        maven {
            url 'https://nrgxnat.jfrog.io/nrgxnat/libs-release'
            name 'XNAT Release Repository'
        }
        maven {
            url 'https://nrgxnat.jfrog.io/nrgxnat/libs-snapshot'
            name 'XNAT Snapshot Repository'
        }
    }
    dependencies {
        classpath "org.nrg.xnat.build:xnat-data-builder:1.7.2"
    }
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'xnat-data-builder'

group = 'edu.example.xnat'
version = '1.0.0-SNAPSHOT'

sourceCompatibility = 1.7
targetCompatibility = 1.7

def javaVersion = Jvm.current().javaVersion
if (javaVersion.java8Compatible || javaVersion.java9Compatible) {
    if (hasProperty("rt.17.jar")) {
        def rt17jar = getProperty("rt.17.jar")
        logger.info "Using ${rt17jar} as the bootstrap class path jar."
        gradle.projectsEvaluated {
            tasks.withType(JavaCompile) {
                options.fork = true
                options.compilerArgs << "-XDignore.symbol.file"
                options.bootClasspath = rt17jar
            }
        }
    } else {
        logger.warn "No value was set for the rt.17.jar build property."
    }
}

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
    maven { url 'http://dcm4che.org/maven2' }
    maven { url 'https://nrgxnat.jfrog.io/nrgxnat/libs-release' }
    maven { url 'https://nrgxnat.jfrog.io/nrgxnat/libs-snapshot' }
    maven { url 'https://nrgxnat.jfrog.io/nrgxnat/ext-release' }
}

def vXnat = '1.7.2'

dependencies {
    compile("org.nrg.xnat:web:${vXnat}") { exclude group: '*' }
    compile("org.nrg.xnat:xnat-data-models:${vXnat}") { exclude group: '*' }
    compile("org.nrg.xdat:core:${vXnat}") { exclude group: '*' }
    compile "org.nrg:prefs:${vXnat}"
    compile "org.nrg:framework:${vXnat}"
}

sourceSets {
    main {
        java {
            srcDir 'src/main/java'
            srcDir 'build/xnat-generated/src/main/java'
        }
        resources {
            srcDir 'src/main/resources'
            srcDir 'build/xnat-generated/src/main/resources'
        }
    }
}


